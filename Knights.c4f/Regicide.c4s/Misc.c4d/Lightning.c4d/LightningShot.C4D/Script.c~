/*-- Blitz --*/

#strict

static lgt_enlightment; // Globale Erhellung durch Blitze
static lgt_last_lgt_frame; // Letztes Frame, in dem die Helligkeit angepasst wurde

/* Locals */

local iAdvX, iVarX, // Bewegung / Abweichung X
      iAdvY, iVarY; // Bewegung / Abweichung Y
local fDoGamma;

/* Aktivierung */

public func Activate(iStartX, iStartY, iAdvanceX, iRandomX, iAdvanceY, iRandomY, fApplyGamma)
{
  // Startposition
  AddVertex(iStartX,iStartY);
  // Bewegungsverhalten
  iAdvX=iAdvanceX; iVarX=iRandomX;
  iAdvY=iAdvanceY; iVarY=iRandomY;
  fDoGamma=fApplyGamma;
  // Aktion setzen
  SetAction("Advance");
  // Geräusch
  if (!Random(5)) Sound("Thunder*");
  // Leuchteffekt
  LightningEffect(10);
  return(1);
}

public Launch:
  var XDir;
  if (Par(0) == DIR_Left()) XDir = -10;
  else (XDir = +10);
  /*YDir = 0;
  SetXDir(XDir);
  SetYDir(YDir);
  XLast = GetX();
  YLast = GetY();
  SetAction("Flying");*/
  Activate(GetX(), GetY(), XDir, 5, 0, 0);
return(1);

/* Bewegung */

private func Advance()
{
  // Einschlag
  var iVtx = GetVertexNum()-1, iVtxX = GetVertex(iVtx, 0), iVtxY = GetVertex(iVtx, 1);
  if (GBackSemiSolid(iVtxX-GetX(), iVtxY-GetY() ))
    return(Remove());

  // Objektschaden
  //if (iVtx>5) BlastObjects(iVtxX, iVtxY, 5);

  // Verzweigung
  if (!Random(35))
    LaunchLightning(iVtxX, iVtxY, iAdvX, iVarX, iAdvY, iVarY);

  // Neuen Eckpunkt
  if (!AddVertex( iVtxX+iAdvX, iVtxY+iAdvY+RandomX(-5,5)))
    return(Remove());

  // Objektanziehung
  iVtx = GetVertexNum()-1; iVtxX = GetVertex(iVtx, 0); iVtxY = GetVertex(iVtx, 1);
  var obj;
  if (iVtx>7)
    if (obj = FindObject( 0, iVtxX-GetX()-50, iVtxY-GetY()-50, 100, 100, OCF_Alive(), 0,0, NoContainer()  ) )
      Attraction(obj);

  LightningEffect(4);

  // Weiter	
  return(ExecLgt());
}

/* Effekte */

private func Sparkle()
{
  var iVtx = GetVertexNum()-1, iVtxX = GetVertex(iVtx, 0), iVtxY = GetVertex(iVtx, 1);
  CastObjects(SPRK,1,20,iVtxX-GetX(), iVtxY-GetY());
  ExecLgt();
  return(1);
}

private func LightningEffect(strength)
  {
  lgt_enlightment+=Random(strength)+strength/2;
  return(1);
  }

private func ExecLgt()
  {
  if (!fDoGamma) return(1);
  // Lichteffekt ausführen (Rampe 5)
  if (lgt_enlightment>0 && lgt_last_lgt_frame != FrameCounter())
    {
    var lgt=Min(lgt_enlightment*3, 255);
    SetGamma(RGB(lgt, lgt, lgt), RGB(128+lgt/2, 128+lgt/2, 128+lgt/2), 16777215, 5);
    lgt_enlightment=Max(lgt_enlightment*4/8-1); lgt_last_lgt_frame=FrameCounter();
    }
  }


/* Anziehung */

private func Attraction(obj)
{
  // Einschlag in Objekt
  AddVertex( GetVertex(0,0,obj)+GetX(obj),
             GetVertex(0,1,obj)+GetY(obj) );
  obj->~LightningStrike();
  SetAction("Connect");
  BlastObjects(GetX(obj), GetY(obj), 5);

AddEffect("Shock", obj, 200, 5, 0, GetID());
  // Leuchteffekt
  LightningEffect(40);
}

/* Ende */

private func Remove()
{
  if (ObjectCount(FXL1)<=1) ResetGamma(5);
  RemoveObject();
}

protected func FxShockStart(pTarget, iEffectNumber)
  {
  // Vorherige Sichtbarkeit des Zauberers speichern
  EffectVar(0, pTarget, iEffectNumber) = 20;
  EffectVar(1, pTarget, iEffectNumber) = GetPhysical("Walk", 0, pTarget);
  EffectVar(2, pTarget, iEffectNumber) = GetPhysical("Swim", 0, pTarget);
  EffectVar(3, pTarget, iEffectNumber) = GetPhysical("Float", 0, pTarget);
  EffectVar(4, pTarget, iEffectNumber) = GetPhysical("Scale", 0, pTarget);
  EffectVar(5, pTarget, iEffectNumber) = GetPhysical("Hangle", 0, pTarget);
  EffectVar(6, pTarget, iEffectNumber) = GetPhysical("Jump", 0, pTarget);
  SetPhysical("Walk", 0, 2, pTarget);
  SetPhysical("Swim", 0, 2, pTarget);
  SetPhysical("Float", 0, 2, pTarget);
  SetPhysical("Scale", 0, 2, pTarget);
  SetPhysical("Hangle", 0, 2, pTarget);
  SetPhysical("Jump", 0, 2, pTarget);
  // Fertig
  return(1);
  }

protected func FxShockTimer(pTarget, iEffectNumber)
  {
  if((--EffectVar(0, pTarget, iEffectNumber))<=0)
  {
	  SetPhysical("Walk", EffectVar(1, pTarget, iEffectNumber), 2, pTarget);
	  SetPhysical("Swim", EffectVar(2, pTarget, iEffectNumber), 2, pTarget);
	  SetPhysical("Float", EffectVar(3, pTarget, iEffectNumber), 2, pTarget);
	  SetPhysical("Scale", EffectVar(4, pTarget, iEffectNumber), 2, pTarget);
	  SetPhysical("Hangle", EffectVar(5, pTarget, iEffectNumber), 2, pTarget);
	  SetPhysical("Jump", EffectVar(6, pTarget, iEffectNumber), 2, pTarget);
	  SetClrModulation(RGB(255, 255, 255), pTarget);
	  if(Random(2)) pTarget->~ContactLeft();
	  else pTarget->~ContactRight();
	  return(-1);
  }
  // Status wiederherstellen
  if(Mod(EffectVar(0, pTarget, iEffectNumber), 2)) SetClrModulation(RGB(255, 255, 255), pTarget);
  else SetClrModulation(RGBa(255, 255, 255, 150), pTarget);
  // Fertig
  return(1);
  }

