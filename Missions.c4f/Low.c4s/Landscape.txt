/* Lavacaves */

overlay AlterMat { mat=Earth; tex=Smooth; turbulence=10; algo=rndchecker; zoomX=-70; zoomY=-70; };

// bounds
overlay Bnds { algo=border; a=1; b=1; mat=Earth; tex=Rough;
  AlterMat { mat=FlyAshes; tex=Smooth; zoomX=-75; zoomY=-75; a=0; };
  AlterMat { mat=Rock; tex=Rough; zoomX=-65; zoomY=-100; a=1; };
};

map CaveParcour {

  // basic shape: earth
  mat=Earth; tex=Ridge;

  // earth altered
  AlterMat;
  AlterMat { tex=Smooth2; a=2; };
  AlterMat { tex=Rough; a=4; };

  // sand in the upper areas
  overlay SandSpots { x=-20; y=0; wdt=140; hgt=60; turbulence=10000; algo=rndchecker; a=6; zoomY=-50; mat=Sand; tex=Smooth; loosebounds=1;
    AlterMat { mat=Sand; tex=Smooth2; zoomX=-60; zoomY=-100; };
    AlterMat { mat=Sand; tex=Smooth3; zoomX=-60; zoomY=-100; a=2; };
  };

  // lava; lots of it
  overlay LavaEx { zoomX=-50; zoomY=-50; algo=rndchecker; mask=1; a=1; turbulence=10000;
    overlay { mat=DuroLava; tex=Liquid; algo=border; a=5; b=5; turbulence=1000; };
  }; LavaEx { a=5; };

  // rock
  overlay InEarth { zoomX=-50; zoomY=-50; algo=rndchecker; a=12; mat=Rock; tex=Rough; turbulence=10000; };
  InEarth { tex=Ridge; };

  // water
  InEarth { mat=Water; tex=Liquid; zoomY=0; a=15; Bnds; };

  // lava
  InEarth { mat=DuroLava; tex=Liquid; };

  // acid
  InEarth { mat=Acid; tex=Liquid; zoomY=0; a=6; Bnds; };

  // wide caves only
  overlay Caves { zoomX=60; zoomY=-20; algo=rndchecker; a=2; turbulence=10000; mat=Tunnel; tex=Smooth; Bnds; };

  // gold
  InEarth { mat=Gold; a=25; };

  // coal
  InEarth { mat=Coal; tex=Swirl; a=25; zoomY=-80; };

  // granite
  InEarth { mat=Granite; a=25; zoomY=-80; overlay { algo=random; a=1; mat=Crystal; tex=Rock; }; };

  // ore
  InEarth { mat=Ore; tex=Ridge; a=28; zoomX=-80; zoomY=-80; };

  // crystal
  InEarth { mat=Crystal; tex=Flare; a=28;
    overlay { algo=random; a=1; mat=Crystal; tex=Structure; };
  };

  // top free
  overlay { turbulence=10000; zoomX=30; zoomY=50; x=-50; y=-50; wdt=200; hgt=70; loosebounds=1; }
  & overlay { turbulence=100; x=35; y=8; wdt=30; hgt=50; loosebounds=1; invert=1; }
  // top bound overlay needs to be put in a operator chain,
  // so the outer loosebounds-turbulence will be applied to the border algo as well
  & overlay {
    // top filled with liquid
    overlay { mat=DuroLava; tex=Liquid; y=8; sub=0; };
    // top rock bound
    overlay { algo=border; a=1; b=1; mat=Rock; tex=Rough; };
    // sandy holes
    overlay { algo=border; a=3; b=2; } & SandSpots { a=1; mat=FlySand; tex=Smooth; loosebounds=0; };
  };

  // top island
  overlay { mat=Earth; tex=Smooth; turbulence=100; x=35; y=8; wdt=30; hgt=5; loosebounds=1; };

  // winning podest
  overlay { mat=Gold; tex=Rough; x=45; y=8; wdt=10; hgt=3; };
  overlay { x=44; y=0; wdt=12; hgt=8; };

  // starting cave
  overlay { x=-20; y=95; wdt=65; hgt=25; mat=Tunnel; tex=Smooth; turbulence=100; loosebounds=1;
    // border
    overlay { algo=border; mat=Ashes; tex=Smooth; };
  };

  // starting pos ensured to be free
  overlay { x=0; y=96; wdt=15; hgt=5; mat=Tunnel; tex=Smooth; };

  // floor
  overlay { y=99; wdt=55; hgt=2; mat=Granite; tex=Rough; };
  };
