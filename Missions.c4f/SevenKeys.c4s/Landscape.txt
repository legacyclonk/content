
map DaLandscape {

  // Meer
  overlay {
    mat=Water; tex=Liquid;
    y=50%; sub=0;
  };

  // Hauptlandschaftsform
	overlay {
		x=30%; wdt=40%;
		algo=sin;
		zoomX=85; zoomY=80; oy=30; ox=20;
    turbulence=10;
		mask=1;
	} | overlay {
		x=30%; wdt=40%;
		algo=sin;
		zoomX=85; zoomY=95; oy=10; ox=20;
    turbulence=10;
		mask=1;
	} | overlay {
    algo=sin;
		zoomX=85; zoomY=65; oy=45; ox= -50 - 50; 
    turbulence=10;
    loosebounds=1;
		mask=1;
		x=-100; wdt=300;

		// Meereseinbettung - Strand
		overlay {
			y=46%; sub=0; turbulence=10; loosebounds=1;
		} & overlay {
			algo=border; a=5; b=5; invert=1; turbulence=1;
		} |	overlay {
			y=48%; sub=0; turbulence=10; loosebounds=1;
			mat=Sand; tex=Smooth;
			overlay {
			  y=3; sub=1; turbulence=10; loosebounds=1;
				mat=Earth; tex=Ridge;
			};
		};
		
		// Erde
		overlay {
			algo=border; a=10; b=10;
			mat=Earth; tex=Ridge;
			invert=1; turbulence=10;
			
			// Texturwechsel
			overlay {
				algo=rndchecker; mat=Earth; tex=Smooth;
				turbulence=100;
			};
			overlay {
				algo=rndchecker; mat=Earth; tex=Rough;
				turbulence=100;
			};
			overlay {
				algo=rndchecker; mat=Earth; tex=Smooth2;
				turbulence=100;
			};			
    
			// Rohstoffe
			overlay mats {				
				algo=rndchecker;
				turbulence=100;
				loosebounds=1;
				a=16; mask=1;
				zoomX=15; zoomY=15;
			};
			
			mats { mask=0; mat=Coal; tex=Swirl; };
			mats { mask=0; mat=Sulphur; tex=Swirl; };
			mats { y=53%; a=20; mask=0; mat=Rock; tex=Ridge; };
			mats { mask=0; mat=Ore; tex=Rock; };
			mats { algo=bozo; turbulence=5000; lambda=2; zoomX=80; zoomY=70; y=70%; a=28; rotate=45; mask=0; mat=Gold; tex=Rough; };
			mats { a=10; mask=0; mat=Granite; tex=Rough; };
		};


		// Bergspitze
		overlay {
			hgt=25; mat=Granite; tex=Rough; loosebounds=1; turbulence=500;
			overlay {
				mat=Rock; tex=Ridge; 
				algo=rndchecker;
				turbulence=100;
				loosebounds=1;
			};
		};
		
		// Himmel (Bergspitze abschneiden)
		overlay {
			hgt=20;
		};

  };

};
